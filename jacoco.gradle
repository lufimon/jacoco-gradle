apply plugin: 'jacoco'

android {
    testOptions {
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }
}

project.afterEvaluate {
    task "createUnitTestCoverageReport"(type: JacocoReport, dependsOn: "testUatDebugUnitTest") {
        reports {
            xml.enabled = true
            html.enabled = true
            xml.destination = file("${buildDir}/reports/jacoco/jacocoTestReport.xml")
            html.destination = file("${buildDir}/reports/jacoco")
        }

        group = "Reporting"
        description = "Generate Jacoco coverage reports on the UatDebug build."

        def fileFilter = [
                '**/*Test*.*',
                '**/AutoValue_*.*',
                '**/*JavascriptBridge.class',
                '**/R.class',
                '**/R$*.class',
                '**/Manifest*.*',
                'android/**/*.*',
                '**/BuildConfig.*',
                '**/*$ViewBinder*.*',
                '**/*$ViewInjector*.*',
                '**/Lambda$*.class',
                '**/Lambda.class',
                '**/*Lambda.class',
                '**/*Lambda*.class',
                '**/*$InjectAdapter.class',
                '**/*$ModuleAdapter.class',
                '**/*$ViewInjector*.class',
                '**/*_MembersInjector.class',
                '*/*_MembersInjector*.*',
                '**/*_*Factory*.*',
                '*/*Component*.*',
                '**/*Module*.*',
                '**/*_Provide*Factory*.*',
                '**/*Dagger*.*',
                '**/*$*$*.*'
        ]

        def javaDebugTree = fileTree(dir: "${buildDir}/intermediates/javac/uatDebug", excludes: fileFilter)

        def kotlinDebugTree = fileTree(dir: "${buildDir}/tmp/kotlin-classes/uatDebug", excludes: fileFilter)

        def mainSrc = "${project.projectDir}/src/main/java"

        sourceDirectories = files([mainSrc])
        classDirectories = files([javaDebugTree, kotlinDebugTree])
        executionData = fileTree(dir: "$buildDir", includes: [
                "jacoco/testUatDebugUnitTest.exec",
                "outputs/code-coverage/connected/*coverage.ec"
        ])
    }
}